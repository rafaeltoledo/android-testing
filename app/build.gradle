apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId 'net.rafaeltoledo.testing'
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    sourceSets {
        final testDir = 'src/test/java'

        test.java.srcDirs = [testDir]
        androidTest.java.srcDirs = [testDir]
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true
        unitTests.includeAndroidResources true
    }
}

def bothTestImplementation = { String dependency ->
    project.dependencies.add('testImplementation', dependency)
    project.dependencies.add('androidTestImplementation', dependency)
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'com.google.android.material:material:1.0.0'

    bothTestImplementation 'androidx.test:core:1.0.0'
    bothTestImplementation 'androidx.test:runner:1.1.0'
    bothTestImplementation 'androidx.test:rules:1.1.0'
    bothTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    bothTestImplementation 'androidx.test.ext:junit:1.0.0'

    testImplementation 'org.robolectric:robolectric:4.0-beta-2'

    androidTestUtil 'androidx.test:orchestrator:1.1.0'
}
